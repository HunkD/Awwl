apply plugin: 'com.android.application'
apply plugin: 'jacoco'
apply from: '../build-common.gradle'

jacoco {
    toolVersion = "0.7.1.201405082137"
}

android {

    compileOptions.encoding = 'utf-8'
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId 'com.hunk.nobank'
        minSdkVersion 10
        targetSdkVersion 22

        versionCode 1
        versionName '1.0'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
        debug {
            minifyEnabled project.hasProperty("proguard")?true:false
            debuggable true
            proguardFile getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFile 'proguard.cfg'
            testCoverageEnabled true
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    "noBank-${project.branchName}-v${variant.versionName}-b${project.buildNumber}-${variant.buildType.name}.apk".replaceAll("/", "-"))
        }
    }

    lintOptions {
        abortOnError false
    }
}

def coverageSourceDirs = [
        '../noBank/src/main/java'
]

task jacocoTestReport(type:JacocoReport, dependsOn: "testDebugUnitTest") {
    group = "Reporting"

    description = "Generate Jacoco coverage reports"

    classDirectories = fileTree(
            dir: '../noBank/build/intermediates/classes/debug',
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/*$ViewInjector*.*',
                       '**/BuildConfig.*',
                       '**/Manifest*.*']
    )

    additionalSourceDirs = files(coverageSourceDirs)
    sourceDirectories = files(coverageSourceDirs)
    executionData = files('../noBank/build/jacoco/testDebugUnitTest.exec',
            '../noBank/build/outputs/code-coverage/connected/coverage.ec')

    reports {
        xml.enabled = true
        html.enabled = true
    }

}

dependencies {
    compile 'com.android.support:support-v4:22.2.0'
    compile 'com.google.code.gson:gson:2.3.1'
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.mcxiaoke.volley:library:1.0.16'

    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:1.7.0'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.robolectric:shadows-support-v4:3.0'

    compile project(':contract')
    compile 'com.squareup.okhttp:okhttp:2.4.0'
}
